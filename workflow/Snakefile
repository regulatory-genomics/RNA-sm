##### global workflow dependencies #####

# ===================================================
#                    Setup Section
# ===================================================

# libraries
import os
import sys
import pandas as pd
from snakemake.utils import validate, min_version

##### set minimum snakemake version #####
min_version("8.0.0")

module_name = "rnaseq_pipeline"

# report: os.path.join("report", "workflow.rst")

# list of names of the used environment specifications in workflow/envs/{env_name}.yaml
envs = ["fastp", "pandas", "multiqc"]

# load config and sample annotation sheets #####
configfile: "config/config.yaml"

validate(config, schema="../schemas/config.schema.yaml")

include: "rules/common.smk"

##### set global variables #####
result_path = config.get("result_path", "results")

# ===================================================
#                    Rules Section
# ===================================================

rule all:
    input:
        # Report outputs
        multiqc_report = os.path.join(result_path, "Report", "multiqc_report.html"),
        
        # Important processed - BAM files (per sample, all runs processed together)
        bam_files = expand(
            os.path.join(result_path, "Important_processed", "Bam", "{sample}_sortedByCoord.out.bam"),
            sample=samples.keys()
        ),
        
        # Downstream results - Count matrix
        counts = os.path.join(result_path, "downstream_res", "counts", "all_counts.tsv"),

        # BigWig files
        bigwigs = get_final_bigwigs,
        # RSEM results
        rsem_genes = expand(
            os.path.join(result_path, "downstream_res", "rsem", "{sample}.genes.results"),
            sample=samples.keys()
        ),
        rsem_isoforms = expand(
            os.path.join(result_path, "downstream_res", "rsem", "{sample}.isoforms.results"),
            sample=samples.keys()
        ),
        gene_type_counts = expand(
            os.path.join(result_path, "Report", "gene_type_counts", "{sample}.json"),
            sample=samples.keys()
        ),
        gene_count_json = expand(
            os.path.join(result_path, "Report", "rsem", "{sample}.genes.json"),
            sample=samples.keys()
        ),
        rnaseqqc = expand(
            [
                os.path.join(result_path, "Report", "rnaseqqc", "{sample}.metrics.tsv"),
                os.path.join(result_path, "Report", "rnaseqqc", "{sample}.exon_reads.gct"),
                os.path.join(result_path, "Report", "rnaseqqc", "{sample}.gene_reads.gct"),
                os.path.join(result_path, "Report", "rnaseqqc", "{sample}.gene_tpm.gct"),
                os.path.join(result_path, "Report", "rnaseqqc", "{sample}.coverage.tsv"),
            ],
            sample=samples.keys()
        ),
        mad_qc = expand(
            os.path.join(result_path, "Report", "mad_qc", "{replicate_name}.summary_metrics.json"),
            replicate_name=replicate_samples
        ) if replicate_samples else [],

##### load rules #####
include: "rules/quantification.smk"
include: "rules/qc.smk"
include: "rules/align.smk"
include: "rules/count.smk"
include: "rules/track.smk"
